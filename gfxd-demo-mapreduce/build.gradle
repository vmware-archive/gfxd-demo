apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'

sourceCompatibility = 1.7
version = '1.0'
mainClassName = 'org.apache.hadoop.util.RunJar'
jobClassName = 'com.pivotal.gfxd.demo.mapreduce.LoadAverage'

jar {
  baseName = "gfxd-demo-mapreduce"
  version = '1.0'
  manifest {
    attributes 'Implementation-Title': 'gfxd-demo-mapreduce',
        'Implementation-Version': version,
        'Main-Class': jobClassName
  }

  doFirst {
    into( 'lib' ) {
      from configurations.runtime
    }
  }
}

configurations {
  runtime.exclude group: "org.slf4j"
}

repositories {
  mavenCentral()
  maven { url "http://repo.spring.io/milestone" }
  maven { url "http://repo.spring.io/libs-snapshot" }
}

run {
  // This is because of weirdness when running from Gradle. The classpath is
  // set to include the build classes, but messes things up for MapReduce.
  doFirst {
    def classesDir = new File("${projectDir}/build/classes")
    classesDir.deleteDir()
  }
  if(project.hasProperty('args')){
    args project.args.split('\\s+')
  }

  // This is only relevant when running on the PHD VM
  classpath += files("/etc/gphd/hadoop/conf")
}

dependencies {
  compile files(GFXD_HOME + '/lib/gemfirexd.jar')
  compile files(GFXD_HOME + '/lib/gemfirexd-client.jar')
  compile 'org.springframework:spring-context:4.0.0.RELEASE'
  compile 'org.apache.hadoop:hadoop-common:2.0.5-alpha'
  compile 'org.apache.hadoop:hadoop-client:2.0.5-alpha'
  compile 'org.slf4j:slf4j-simple:1.7.5'

  runtime project(path: ":gfxd-demo-aeq-listener", configuration: "aeqListenerJar")
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.10'
}
